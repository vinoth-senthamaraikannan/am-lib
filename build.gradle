plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'java'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
  id 'org.springframework.boot' version '2.1.0.RELEASE'
  id 'org.sonarqube' version '2.6.2'
  id 'org.owasp.dependencycheck' version '4.0.2'
  id 'com.github.ben-manes.versions' version '0.20.0'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'
def amLibJarVersion = '0.0.3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'java'

sourceSets {
  test {
    java.srcDir 'src/test/java'
  }
  functional {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/test/functional/java')
    }
    resources.srcDir file('src/test/resources')
  }

  test {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/test/java')
    }
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  // set your environment variables here
  // environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

configurations {
  functionalCompile.extendsFrom testCompile
  functionalRuntime.extendsFrom testRuntime
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnit {
    includeCategories 'uk.gov.hmcts.reform.amapi.functional.SmokeTest'
  }
}

checkstyle {
  maxWarnings = 0
  toolVersion = '8.14'
  // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
  configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
  toolVersion = "6.9.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functional, sourceSets.integrationTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn test, integration

sonarqube {
  properties {
    property "sonar.projectName", "Reform :: am-lib-testing-service"
    property "sonar.projectKey", "uk.gov.hmcts.reform:am-lib-testing-service"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"

  }
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = 11 //System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
}

repositories {
  jcenter()
}

// it is important to specify logback classic and core packages explicitly as libraries like spring boot
// enforces it's own (older) version which is not recommended.
def versions = [
  junit: '5.3.1',
  reformLogging: '3.0.2',
  springBoot: springBoot.class.package.implementationVersion,
  springfoxSwagger: '2.9.2'
]

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
  compile files("am-lib-${amLibJarVersion}.jar")
  testCompile files("am-lib-${amLibJarVersion}.jar")

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformLogging
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.0.2.RELEASE'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
  testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.3.0'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot

  integrationTestCompile sourceSets.main.runtimeClasspath
  integrationTestCompile sourceSets.test.runtimeClasspath

  functionalCompile sourceSets.main.runtimeClasspath
}

mainClassName = 'uk.gov.hmcts.reform.amapi.Application'

bootJar {
  archiveName = 'am-lib-testing-service.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

task testLib(type:Exec) {
  workingDir './am-lib'
  commandLine './gradlew', 'test'
}

task buildLib(type:Exec) {
  workingDir './am-lib'
  commandLine './gradlew', 'jar'
}

task copyLib(type:Exec) {
  workingDir '.'
  commandLine 'cp', "./am-lib/build/libs/am-lib-${amLibJarVersion}.jar", '.'
}

test.dependsOn testLib
compileJava.dependsOn copyLib
copyLib.dependsOn buildLib
