plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'info.solidsoft.pitest' version '1.4.0'
}

group 'uk.gov.hmcts.reform'
version "${version}"

configurations {
    testOutput.extendsFrom(testCompile)
}

pitest {
    targetTests = ['integration.uk.gov.hmcts.reform.amlib.*']
    excludedTestClasses = ['integration.uk.gov.hmcts.reform.amlib.IntegrationBaseTest']
    excludedClasses = ['uk.gov.hmcts.reform.amlib.models.*']
    threads = 4
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
}

checkstyle {
    toolVersion = '8.17'
    maxWarnings = 0
    configDir = new File("config/checkstyle")
}

pmd {
    toolVersion = "6.11.0"
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$buildDir/reports/pmd/$project.name")
    ruleSetFiles = files("$projectDir/config/pmd/ruleset.xml")
    ruleSets = []
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
    
    compile group: 'org.jdbi', name: 'jdbi3-core', version: '3.6.0'
    compile group: 'org.jdbi', name: 'jdbi3-sqlobject', version: '3.6.0'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.24.0'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.12.0'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.197'
    testCompile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'
}

task codeCoverageReport(type: JacocoReport, dependsOn: test) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        csv.enabled = false
        html.enabled = true
        html.destination = file("${buildDir}/reports/jacoco/html")
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacoco/jacocoTestReport.xml")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task jarTest(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output
}

shadowJar {
    from('src/main/java') {
        include '**/*.sql'
    }
    classifier = 'all'
}

artifacts {
    archives shadowJar
    archives javadocJar
    testOutput jarTest
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact shadowJar
            artifact javadocJar
            groupId project.group
            artifactId 'am-lib'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'am-lib'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/am-lib'
        version {
            name = project.version
        }
    }
}
